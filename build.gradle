buildscript {
    apply from: "versions.gradle"
}

plugins {
    id 'com.palantir.git-version' version '0.11.0' apply(false)
}

if (!project.hasProperty('revision')) {
    apply plugin: 'com.palantir.git-version'
}

allprojects {
    group 'com.github.the20login'
    version '0.0.1'
    ext {
        revision = project.hasProperty('revision') ? project.property('revision') : versionDetails().gitHashFull
    }
}

subprojects {
    repositories {
        mavenCentral()
    }

    tasks.withType(AbstractArchiveTask) {
        preserveFileTimestamps = false
        reproducibleFileOrder = true
        manifest {
            attributes(
                    'Build-Revision' : revision,
                    'Build-Version' : archiveVersion
            )
        }
    }

    tasks.withType(Test) {
        systemProperty "revision", revision
    }
}

task resolveDependencies {
    setDescription "Resolves all projects dependencies from the repository."
    setGroup "Build Server"

    doLast {
        rootProject.allprojects { project ->
            project.buildscript.configurations.forEach { configuration ->
                if (configuration.canBeResolved) {
                    configuration.resolve()
                }
            }

            project.configurations.forEach { configuration ->
                if (configuration.canBeResolved) {
                    configuration.resolve()
                }
            }
        }
    }
}
